plugins {
    id 'java'
    id 'maven'
    id 'jacoco'
    id 'io.franzbecker.gradle-lombok' version '1.11'
}

group 'be.bendem'
version '0.0.1'

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile('org.springframework:spring-context:4.3.13.RELEASE')
    compile('org.springframework:spring-tx:4.3.13.RELEASE')
    compile('org.springframework.boot:spring-boot-autoconfigure:1.5.8.RELEASE')
    compile('be.bendem:sql-streams:0.1.1')
}

lombok { // optional: values below are the defaults
    version = "1.16.18"
    sha256 = "9d957f572386b9e257093a45b148f9b411cff80d9efd55eaf6fca27002d2e4d9"
}

import io.franzbecker.gradle.lombok.task.DelombokTask

task delombok(type: DelombokTask, dependsOn: compileJava) {
    ext.outputDir = file("$buildDir/delombok")
    outputs.dir(outputDir)
    sourceSets.main.java.srcDirs.each {
        inputs.dir(it)
        args(it, "-d", outputDir)
    }
}

task delombokHelp(type: DelombokTask) {
    args "--help"
}

install {
    repositories.mavenInstaller {
        pom.artifactId = 'sql-streams-spring'
    }
}

javadoc {
    dependsOn delombok
    source = delombok.outputDir
    options.encoding = 'UTF-8'
    options.links 'https://docs.oracle.com/javase/8/docs/api/'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}
